WHITESPACE = _{ (" " | "\r\n" | "\n")+ }
COMMENT = _{ ("//" ~ (!"\n" ~ ANY)*) | ("/*" ~ (!"*/" ~ ANY)* ~ "*/") }

null = { "null" }
bool = { "true" | "false" }
number = { ASCII_DIGIT+ }

identifier = { (ASCII_ALPHA | "_") ~ (ASCII_ALPHA | ASCII_DIGIT | "_")* }
atom = { null | bool | number | identifier | ("(" ~ expression ~ ")") }

assignmentOperator = {
  ":=" | "=" | "**=" | "+=" | "++=" | "-=" | "*=" | "/=" | "%=" | "<<=" | ">>=" | "&=" |
  "|=" | "^=" | "&&=" | "||=" | "~="
}

binaryOperator = {
  "**" |
  "*" | "/" | "%" |
  "++" | "+" | "-" |
  "<<" | ">>" |
  "<=" | ">=" | "<" | ">" |
  "==" | "!=" |
  "~" |
  "&" |
  "^" |
  "|" |
  "&&" |
  "||" |
  "in"
}

assignmentTarget = { identifier }

expression = { atom ~ (binaryOperator ~ atom)* }

block = { "{" ~ statement* ~ "}" }

functionStatement = { "fn" ~ identifier ~ "()" ~ "{" ~ statement* ~ "}" }
assignmentStatement = { assignmentTarget ~ assignmentOperator ~ expression ~ ";" }

keywordStatement = {
  (
    "breakpoint" |
    "break" |
    "continue"
  ) ~ ";"
}

keywordExpressionStatement = {
  (
    "return" |
    "assert" |
    ("log." ~ ("info" | "warn" | "error"))
  ) ~ expression ~ ";"
}

ifStatement = {
  "if" ~ "(" ~ expression ~ ")" ~ block ~ elseClause?
}

elseClause = {
  "else" ~ (ifStatement | block)
}

statement = {
  functionStatement |
  keywordStatement |
  keywordExpressionStatement |
  ifStatement |
  assignmentStatement
}

file = { SOI ~ statement* ~ EOI }
