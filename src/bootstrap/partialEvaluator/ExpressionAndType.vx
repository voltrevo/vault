import @/parser/expressionTypes.vx;
import @/parser/operatorTypes.vx;
import @/util/kinds.vx;
import @/util/Join.vx;

import ./Eval/Eval.vx;
import ./FunctionAndType.vx;
import ./Type.vx;

return func ExpressionAndType(expression) {
  [xType, data, pos] := expression;

  if (xType == 'literal') {
    return {
      exp: expression,
      type: ['literal', expression[1]],
    };
  }

  if (xType in #[
    'import', 'default', 'identifier', 'op',
  ]) {
    return {
      exp: expression,
      type: Type.Any, // TODO: More inference here
    };
  }

  if (xType in expressionTypes.operator) {
    if (xType in operatorTypes.mutation) {
      if (xType in #['unary ++', 'unary --']) {
        [target] := data;
        left := ExpressionAndType(target);

        if (left.type[0] == 'never') {
          return left;
        }

        // Preserve the original target
        left.exp = target;

        assert left.exp[0] != 'literal';

        leftType := left.type;
        left.type = Type.Intersection(left.type, ['kinds', kinds.number]);

        if (left.type[0] == 'never') {
          return {
            exp: null,
            type: ['never', {
              level: 'error',
              message: (
                'operand of ' ++ xType ++ ' is a ' ++ Type.String(leftType)
              ),
              pos,
              subnotes: [],
              tags: #['type-error'],
            }],
          };
        }

        return {exp: [xType, [left.exp], pos], type: left.type};
      }

      target := data[0];
      [left, right] := data:map(ExpressionAndType);

      if (left.type[0] == 'never') {
        return left;
      }

      if (right.type[0] == 'never') {
        return right;
      }

      if (xType in #[':=', '=']) {
        // TODO: Destructure type checking
        return {
          exp: [xType, [target, right.exp], pos],
          type: ['na', null],
        };
      }

      assert target[0] != 'literal';

      op := switch (xType) {
        '**=' => '**';
        '+=' => '+';
        '++=' => '++';
        '-=' => '-';
        '*=' => '*';
        '/=' => '/';
        '%=' => '%';
        '<<=' => '<<';
        '>>=' => '>>';
        '&=' => '&';
        '|=' => '|';
        '^=' => '^';
        '&&=' => '&&';
        '||=' => '||';
        '~=' => '~';
      };

      resType := Eval(op, [left.type, right.type]).output;

      if (resType[0] == 'never') {
        return {
          exp: null,
          type: ['never', {
            level: 'error',
            message: (
              'Invalid operands of ' ++
              xType ++
              ': left is ' ++
              Type.String(left.type) ++
              ', right is ' ++
              Type.String(right.type)
            ),
            pos,
            subnotes: [],
            tags: #['type-error'],
          }],
        };
      }

      // TODO: Scope update for lhs -> resType

      return {
        exp: [xType, [target, right.exp], pos],
        type: ['na', null],
      };
    }

    if (xType == 'functionCall') {
      [fn, args] := data;

      fn = ExpressionAndType(fn);
      args = args:map(ExpressionAndType);

      for (input of [fn] ++ args) {
        if (input.type[0] == 'never') {
          return input;
        }
      }

      argTypes := args:map(func(a) => a.type);

      resType := Eval('functionCall', [
        fn.type,
        argTypes,
      ]).output;

      if (resType[0] == 'never') {
        return {
          exp: null,
          type: ['never', {
            level: 'error',
            message: (
              'Invalid functionCall: left: ' ++
              Type.String(fn.type) ++
              ', args: ' ++
              switch (args:Length()) {
                0u64 => '(none)';
                default => Join(', ')(argTypes:map(Type.String));
              }
            ),
            pos,
            subnotes: [],
            tags: #['type-error'],
          }],
        };
      }

      if (resType[0] == 'literal') {
        return {
          exp: ['literal', resType[1], pos],
          type: resType,
        };
      }

      for (i := 0u64; i < args:Length(); i++) {
        if (args[i].type[0] == 'literal') {
          args[i].exp = ['literal', args[i].type[1], args[i].exp[2]];
        }
      }

      return {
        exp: ['functionCall', [fn.exp, args:map(func(a) => a.exp)], pos],
        type: resType,
      };
    }

    if (xType in operatorTypes.unary) {
      [target] := data:map(ExpressionAndType);

      resType := Eval(xType, [target.type]).output;

      if (resType[0] == 'never') {
        return {
          exp: null,
          type: ['never', {
            level: 'error',
            message: (
              'Invalid operand of ' ++
              xType ++
              ': ' ++
              Type.String(target.type)
            ),
            pos,
            subnotes: [],
            tags: #['type-error'],
          }],
        };
      }

      if (resType[0] == 'literal') {
        return {
          exp: ['literal', resType[1], pos],
          type: resType,
        };
      }

      return {
        exp: [xType, [target.exp], pos],
        type: resType,
      };
    }

    [left, right] := data:map(ExpressionAndType);

    if (left.type[0] == 'never') {
      return left;
    }

    if (right.type[0] == 'never') {
      return right;
    }

    resType := Eval(xType, [left.type, right.type]).output;

    if (resType[0] == 'never') {
      return {
        exp: null,
        type: ['never', {
          level: 'error',
          message: (
            'Invalid operands of ' ++
            xType ++
            ': left: ' ++
            Type.String(left.type) ++
            ', right: ' ++
            Type.String(right.type)
          ),
          pos,
          subnotes: [],
          tags: #['type-error'],
        }],
      };
    }

    if (resType[0] == 'literal') {
      return {
        exp: ['literal', resType[1], pos],
        type: resType,
      };
    }

    return {
      exp: [xType, [left.exp, right.exp], pos],
      type: resType,
    };
  }

  if (xType in expressionTypes.container) {
    if (xType == 'object') {
      typeData := {};
      literal := {};

      // TODO: fix incorrect unused warning
      for ([i, [key, value]] of data:Entries()) {
        {exp, type} := ExpressionAndType(value);

        if (type[0] == 'never') {
          return {exp: null, type};
        }

        if (type[0] != 'literal') {
          literal = null;
        } else if (literal:Kind() != 'null') {
          literal[key] := type[1];
        }

        data[i][1] = exp;
        typeData[key] := type;
      }

      return switch (literal:Kind()) {
        'object' => {
          exp: ['literal', literal, pos],
          type: ['literal', literal],
        };

        'null' => {
          exp: ['object', data, pos],
          type: ['object', typeData],
        };
      };
    }

    if (xType == 'set') {
      literal := #[];

      elType := ['never', null];

      for ([i, {exp, type}] of data:map(ExpressionAndType):Entries()) {
        if (type[0] == 'never') {
          return {exp: null, type};
        }

        if (Type.Kinds(type) == #['func']) {
          return {
            exp: null,
            type: ['never', {
              level: 'error',
              message: 'Invalid set element type: ' ++ Type.String(type),
              pos: exp[2],
              subnotes: [],
              tags: #['type-error'],
            }],
          };
        }

        if (type[0] != 'literal') {
          literal = null;
        } else if (literal:Kind() != 'null') {
          literal |= #[type[1]];
        }

        data[i] = exp;
        elType = Type.Union(elType, type);
      }

      return switch (literal:Kind()) {
        'set' => {
          exp: ['literal', literal, pos],
          type: ['literal', literal],
        };

        'null' => {
          exp: ['set', data, pos],
          type: ['set', elType],
        };
      };
    }

    if (xType == 'array') {
      literal := [];
      typeData := [];

      for ([i, {exp, type}] of data:map(ExpressionAndType):Entries()) {
        if (type[0] == 'never') {
          return {exp: null, type};
        }

        if (type[0] != 'literal') {
          literal = null;
        } else if (literal:Kind() != 'null') {
          literal ++= [type[1]];
        }

        data[i] = exp;
        typeData ++= [type];
      }

      return switch (literal:Kind()) {
        'array' => {
          exp: ['literal', literal, pos],
          type: ['literal', literal],
        };

        'null' => {
          exp: ['array', data, pos],
          type: ['array', typeData],
        };
      };
    }

    return switch{};
  }

  if (xType in expressionTypes.nameAccess) {
    [base, name] := data;
    base = ExpressionAndType(base);

    if (base.type[0] == 'never') {
      return base;
    }

    if (xType == '.') {
      resType := Eval('subscript', [base.type, ['literal', name]]).output;

      if (resType[0] == 'never') {
        return {
          exp: null,
          type: ['never', {
            level: 'error',
            message: (
              '.' ++
              name ++
              ' failed on type ' ++
              Type.String(base.type)
            ),
            pos,
            subnotes: [],
            tags: #['type-error'],
          }],
        };
      }

      return {
        exp: ['.', [base.exp, name], pos],
        type: resType,
      };
    }

    if (xType == 'methodLookup') {
      baseKinds := Type.Kinds(base.type);

      baseCapture := base;

      func BadMethod() => {
        exp: null,
        type: ['never', {
          level: 'error',
          message: (
            'Type ' ++
            Type.String(baseCapture.type) ++
            ' does not have method :' ++
            name
          ),
          pos,
          subnotes: [],
          tags: #['type-error'],
        }],
      };

      if (name == 'Kind') {
        baseKindsValues := baseKinds:Values();

        if (baseKindsValues:Length() == 1u64) {
          [kind] := baseKindsValues;

          return {
            exp: ['methodLookup', [base.exp, name], pos],
            type: ['func', {args: [], output: ['literal', kind]}],
          };
        }

        return {
          exp: ['methodLookup', [base.exp, name], pos],
          type: ['func', {args: [], output: ['kinds', #['string']]}],
        };
      }

      if (name == 'String') {
        if (baseKinds == #['func']) {
          return BadMethod();
        }

        if (base.type[0] == 'literal') {
          return {
            exp: ['methodLookup', [base.exp, name], pos],
            type: ['func', {
              args: [],
              output: ['literal', base.type[1]:String()]
            }],
          };
        }

        hasFunc := (
          Type.IntersectKindsDeep(base.type, kinds.all ~ #['func'])[0] ==
          'never'
        );

        outputType := switch (hasFunc) {
          true => ['never', null];
          false => ['kinds', #['string']];
        };

        return {
          exp: ['methodLookup', [base.exp, name], pos],
          type: ['func', {args: [], output: outputType}],
        };
      }

      if (name == 'Length') {
        if ((baseKinds & #['string', 'array']) == #[]) {
          return BadMethod();
        }

        return {
          exp: ['methodLookup', [base.exp, name], pos],
          type: ['func', {
            args: [],
            output: switch {
              (base.type[0] in #['array', 'literal']) => (
                ['literal', base.type[1]:Length()]
              );

              default => ['kinds', #['u64']];
            },
          }],
        };
      }

      if (name in #[
        'Keys', 'Entries', 'Row', 'Column', 'Transpose',
      ]) {
        if ((baseKinds & #['array', 'object']) == #[]) {
          return BadMethod();
        }

        func KeyType() {
          keyKinds := #[];

          if ('array' in baseKinds) {
            keyKinds |= #['u64'];
          }

          if ('object' in baseKinds) {
            keyKinds |= #['string'];
          }

          return ['kinds', keyKinds];
        };

        if (name == 'Keys') {
          if (base.type[0] in #['array', 'object', 'literal']) {
            return {
              exp: ['methodLookup', [base.exp, name], pos],
              type: ['func', {
                args: [],
                output: ['literal', base.type[1]:Keys()],
              }],
            };
          }

          return {
            exp: ['methodLookup', [base.exp, name], pos],
            type: ['func', {
              args: [],
              output: ['set', KeyType()],
            }],
          };
        }

        if (name == 'Entries') {
          output := switch {
            (base.type[0] in #['literal']) => (
              ['literal', base.type[1]:Entries()]
            );

            (base.type[0] in #['array', 'object']) => (
              ['array',
                base.type[1]:Entries():map(func([key, valueType]) => (
                  switch (valueType[0]) {
                    'literal' => ['literal', [key, valueType[1]]];

                    default => ['array', [
                      ['literal', key],
                      valueType,
                    ]];
                  }
                )),
              ]
            );

            (base.type[0] in #['array-uniform']) => (
              ['array-uniform',
                ['array', [
                  ['kinds', #['u64']],
                  [base.type[1]],
                ]],
              ]
            );

            (base.type[0] in #['kinds']) => (
              ['array-uniform', ['array', [KeyType(), Type.Any]]]
            );
          };

          return {
            exp: ['methodLookup', [base.exp, name], pos],
            type: ['func', {args: [], output}],
          };
        }

        if (name == 'Row') {
          return {
            exp: ['methodLookup', [base.exp, name], pos],
            type: ['func', {
              args: [],
              output: switch (base.type[0]) {
                'literal' => ['literal', [base.type[1]]];
                default => ['array', [base.type]];
              },
            }],
          };
        }

        if (name in #['Column', 'Transpose']) {
          // TODO: more return type inference

          return {
            exp: ['methodLookup', [base.exp, name], pos],
            type: ['func', {args: [], output: ['kinds', #['array', 'object']]}],
          };
        }

        return switch{};
      }

      if (name == 'Values') {
        if ((baseKinds & #['array', 'object', 'set']) == #[]) {
          return BadMethod();
        }

        if (base.type[0] == 'kinds') {
          return {
            exp: ['methodLookup', [base.exp, name], pos],
            type: ['func', {args: [], output: ['kinds', #['array']]}],
          };
        }

        if (base.type[0] == 'array') {
          return {
            exp: ['methodLookup', [base.exp, name], pos],
            type: ['func', {args: [], output: base.type}],
          };
        }

        if (base.type[0] == 'object') {
          return {
            exp: ['methodLookup', [base.exp, name], pos],
            type: ['func', {
              args: [],
              output: ['array', base.type[1]:Values()],
            }],
          };
        }

        if (base.type[0] in #['array-uniform', 'set']) {
          return {
            exp: ['methodLookup', [base.exp, name], pos],
            type: ['func', {
              args: [],
              output: ['array-uniform', base.type[1]],
            }],
          };
        }

        return switch{};
      }

      if (name in #['Front', 'Back', 'map', 'reduce']) {
        if (!('array' in baseKinds)) {
          return BadMethod();
        }

        if (name in #['Front', 'Back']) {
          if (base.type[0] == 'kinds') {
            return {
              exp: ['methodLookup', [base.exp, name], pos],
              type: ['func', {
                args: [],
                output: Type.Any,
              }],
            };
          }

          if (base.type[0] == 'array') {
            return {
              exp: ['methodLookup', [base.exp, name], pos],
              type: ['func', {
                args: [],
                output: switch (name) {
                  'Front' => base.type[1]:Front();
                  'Back' => base.type[1]:Back();
                },
              }],
            };
          }

          if (base.type[0] == 'array-uniform') {
            return {
              exp: ['methodLookup', [base.exp, name], pos],
              type: ['func', {
                args: [],
                output: base.type[1],
              }],
            };
          }

          if (base.type[0] == 'literal') {
            if (base.type[1]:Length() == 0u64) {
              return {
                exp: ['methodLookup', [base.exp, name], pos],
                type: ['func', {
                  args: [],
                  output: ['never', {
                    level: 'error',
                    message: 'Can\'t access :' ++ name ++ '() of empty array',
                    pos,
                    subnotes: [],
                    tags: #['type-error'],
                  }],
                }],
              };
            }

            return {
              exp: ['methodLookup', [base.exp, name], pos],
              type: ['func', {
                args: [],
                output: switch (name) {
                  'Front' => base.type[1]:Front();
                  'Back' => base.type[1]:Back();
                },
              }],
            };
          }

          return switch{};
        }

        if (name == 'map') {
          // TODO: Element typing of the array output depends on the mapper
          // function passed in, which is inaccessible from here and requires
          // more context. It might be worth specializing on :map() expressions
          // rather than simply :map being done here, which doesn't necessarily
          // know that it's going to be immediately called.

          if (base.type[0] == 'kinds') {
            return {
              exp: ['methodLookup', [base.exp, name], pos],
              type: ['func', {
                args: [['func', {
                  args: [Type.Any],
                  output: Type.Any,
                }]],
                output: ['kinds', #['array']],
              }],
            };
          }

          if (base.type[0] in #['array', 'literal']) {
            if (base.type[1]:Length() == 0u64) {
              return {
                exp: ['methodLookup', [base.exp, name], pos],
                type: ['func', {
                  args: [['func', {
                    args: [Type.Any],
                    output: Type.Any,
                  }]],
                  output: ['literal', []],
                }],
              };
            }

            elType := switch (base.type[0]) {
              'array' => base.type[1]:reduce(Type.Union);

              'literal' => (base.type[1]
                :map(func(v) => ['literal', v])
                :reduce(Type.Union)
              );
            };

            return {
              exp: ['methodLookup', [base.exp, name], pos],
              type: ['func', {
                args: [['func', {
                  args: [elType],
                  output: Type.Any,
                }]],
                output: ['array', base.type[1]:map(func(_) => Type.Any)],
              }],
            };
          }

          if (base.type[0] == 'array-uniform') {
            return {
              exp: ['methodLookup', [base.exp, name], pos],
              type: ['func', {
                args: [['func', {
                  args: [base.type[1]],
                  output: Type.Any,
                }]],
                output: ['array-uniform', Type.Any],
              }],
            };
          }

          return switch{};
        }

        if (name == 'reduce') {
          // TODO: Similar to :map, it might be worth specializing on
          // :reduce().

          if (base.type[0] == 'kinds') {
            return {
              exp: ['methodLookup', [base.exp, name], pos],
              type: ['func', {
                args: [['func', {
                  args: [Type.Any, Type.Any],
                  output: Type.Any,
                }]],
                output: Type.Any,
              }],
            };
          }

          if (base.type[0] in #['array', 'literal']) {
            if (base.type[1]:Length() == 0u64) {
              return {
                exp: ['methodLookup', [base.exp, name], pos],
                type: ['func', {
                  args: [['func', {
                    args: [Type.Any, Type.Any],
                    output: Type.Any,
                  }]],
                  output: ['never', {
                    level: 'error',
                    message: 'Can\'t :reduce on an empty array',
                    pos,
                    subnotes: [],
                    tags: #['type-error'],
                  }],
                }],
              };
            }

            elType := switch (base.type[0]) {
              'array' => base.type[1]:reduce(Type.Union);

              'literal' => (base.type[1]
                :map(func(v) => ['literal', v])
                :reduce(Type.Union)
              );
            };

            if (base.type[1]:Length() == 1u64) {
              return {
                exp: ['methodLookup', [base.exp, name], pos],
                type: ['func', {
                  args: [['func', {
                    // TODO: Is the reducer actually required to accept elType
                    // if it's already just one value and the reducer never
                    // actually gets called? This is also relevant to the
                    // array-uniform case!
                    args: [elType, elType],
                    output: Type.Any,
                  }]],
                  output: elType,
                }],
              };
            }

            return {
              exp: ['methodLookup', [base.exp, name], pos],
              type: ['func', {
                args: [['func', {
                  args: [elType, elType],
                  output: Type.Any,
                }]],
                output: Type.Any,
              }],
            };
          }

          if (base.type[0] == 'array-uniform') {
            return {
              exp: ['methodLookup', [base.exp, name], pos],
              type: ['func', {
                args: [['func', {
                  args: [base.type[1], base.type[1]],
                  output: Type.Any,
                }]],
                output: Type.Any,
              }],
            };
          }

          return switch{};
        }

        return switch{};
      }

      if (name == 'bind') {
        if (!('func' in baseKinds)) {
          return BadMethod();
        }

        if (base.type[0] == 'kinds') {
          return {
            exp: ['methodLookup', [base.exp, name], pos],
            type: ['func', {
              args: [Type.Any],
              output: ['kinds', #['func']],
            }],
          };
        }

        if (base.type[0] == 'func') {
          if (base.type[1].args:Length() == 0u64) {
            output := ['never', {
              level: 'error',
              message: (
                'Can\'t bind function that doesn\'t take any arguments: ' ++
                Type.String(base.type)
              ),
              pos,
              subnotes: [],
              tags: #['type-error'],
            }];

            return {
              exp: ['methodLookup', [base.exp, name], pos],
              type: ['func', {args: [output], output}],
            };
          }

          args := [base.type[1].args[0]];

          nextArgs := [];

          // TODO: Really need to add slice operations
          for (i := 1u64; i < base.type[1].args:Length(); i++) {
            nextArgs ++= [base.type[1].args[i]];
          }

          return {
            exp: ['methodLookup', [base.exp, name], pos],
            type: ['func', {
              args,
              output: ['func', {
                args: nextArgs,
                output: base.type[1].output,
              }],
            }],
          };
        }

        return switch{};
      }

      return BadMethod();
    }

    return switch{};
  }

  if (xType == 'switch') {
    {test, cases} := data;

    resType := ['never', null];

    if (test[0] != 'nothing') {
      test = ExpressionAndType(test);

      if (test.type[0] == 'never') {
        return test;
      }
    } else {
      test = {
        exp: test,
        type: null,
      };
    }

    newCases := [];

    for ([left, right] of cases) {
      right = ExpressionAndType(right);

      if (right.type[0] == 'never') {
        return right;
      }

      if (left[0] == 'default') {
        newCases ++= [[left, right.exp]];
        resType = Type.Union(resType, right.type);

        // TODO: Errors from unreachable cases might still be valuable. How
        // should they be surfaced?
        break;
      }

      left = ExpressionAndType(left);

      if (left.type[0] == 'never') {
        return left;
      }

      specType := switch (test.exp[0]) {
        'nothing' => left.type;
        default => Eval('==', [test.type, left.type]).output;
      };

      if (specType[0] == 'never') {
        return {
          exp: null,
          type: ['never', {
            level: 'error',
            message: (
              'Incompatible types in switch comparison: test: ' ++
              Type.String(test.type) ++
              ', case: ' ++
              Type.String(left.type)
            ),
            pos: left.exp[2],
            subnotes: [],
            tags: #['type-error'],
          }],
        };
      }

      if (!('bool' in Type.Kinds(specType))) {
        return {
          exp: null,
          type: ['never', {
            level: 'error',
            message: 'Non-bool test case: ' ++ Type.String(specType),
            pos: left.exp[2],
            subnotes: [],
            tags: #['type-error'],
          }],
        };
      }

      if (specType[0] == 'literal') {
        if (specType[1]) {
          newCases ++= [
            ['default', null, left.exp[2]],
            right.exp,
          ];

          resType = Type.Union(resType, right.type);

          break;
        } else {
          // Don't include unreachable case (TODO: warn?)
        }
      } else {
        newCases ++= [[left.exp, right.exp]];
        resType = Type.Union(resType, right.type);
      }
    }

    cases = newCases;

    if (cases:Length() == 1u64) {
      [[[leftType, _, _], right]] := cases;
      if (leftType == 'default') {
        return {exp: right, type: resType};
      }
    }

    return {
      exp: ['switch', {test: test.exp, cases}, pos],
      type: resType,
    };
  }

  if (xType == 'func') {
    return FunctionAndType(expression);
  }

  return switch{};
};
